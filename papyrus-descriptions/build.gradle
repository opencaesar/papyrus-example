/* 
 * The Maven coordinates for the project artifact
 */
description='The Paprysus example descriptions project'
group = 'io.opencaesar.ontologies'
version = '0.1.0'

/* 
 * The Gradle task dependencies 
 */
buildscript {
	repositories {
		mavenLocal()
		maven { url 'https://dl.bintray.com/opencaesar/papyrus-adapter' }
		maven { url 'https://dl.bintray.com/opencaesar/owl-adapter' }
		maven { url 'https://dl.bintray.com/opencaesar/owl-tools' }
		maven { url 'https://dl.bintray.com/opencaesar/oml-tools' }
		maven { url 'https://dl.bintray.com/opencaesar/oml' }
		maven { url 'https://dl.bintray.com/vermeulen-mp/gradle-plugins' }
		jcenter()
	}
	dependencies {
		classpath 'io.opencaesar.owl:owl-reason-gradle:+'
		classpath 'io.opencaesar.adapters:oml2owl-gradle:0.8.+'
		classpath 'io.opencaesar.oml:oml-merge-gradle:0.8.+'
		classpath 'io.opencaesar.adapters:papyrus2oml-gradle:0.8.+'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
		// needed since gradle-bintray-plugin brings an old version of xerces
		configurations.classpath.exclude group: 'xerces', module: 'xercesImpl'
	}
}

/*
 * Dependency versions
 */
ext { 
	papyrusVocabulariesVersion = '0.+'
}

/*
 * The configuration of OML dependencies
 */
configurations {
	oml
}

/*
 * The repositories to look up OML dependencies in
 */
repositories {
	mavenLocal()
	maven { url 'https://dl.bintray.com/opencaesar/ontologies' }
	jcenter()
}

/*
 * The OML dependencies
 */
dependencies {
	oml "io.opencaesar.ontologies:papyrus-vocabularies:$papyrusVocabulariesVersion"
}

/*
 * A task to extract and merge the OML dependencies
 */
task downloadDependencies(type:io.opencaesar.oml.merge.OmlMergeTask) {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to convert a Papyrus UML model with a DSL profile applied to OML description bundle
 */
task papyrusToOml(type:io.opencaesar.papyrus2oml.Papyrus2OmlTask, dependsOn: downloadDependencies) {
	// Papyrus folder
	inputFolderPath = file('src/papyrus')
	// Papyrus root model
	inputModelPath = file('src/papyrus/opencaesar.io/firesat/firesat.uml')
	// OML catalog
	outputCatalogPath = file('catalog.xml')
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask, dependsOn: papyrusToOml) {
	// OML catalog
	inputCatalogPath = file('catalog.xml')
	// OWL catalog
	outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to build the project, which executes several tasks together
 */
task build() {
	dependsOn omlToOwl
}

/*
 * A task to delete the build artifacts
 */
task clean(type: Delete) {
	delete 'build'
}

/*
 * Publish to Maven spec
 */
apply plugin: 'maven-publish'

task omlZip(type: Zip) {
	from file('build/oml')
	include "opencaesar.io/firesat/**/*.oml"
	destinationDirectory = file('build/libs')
	archiveBaseName = project.name
	archiveVersion = project.version
}

publishToMavenLocal.dependsOn omlZip

publishing {
    publications {
        maven(MavenPublication) {
            artifact omlZip
			pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.oml.allDependencies.stream().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }
}

/*
 * Publish to Bintrary Spec
 */
apply plugin: 'com.jfrog.bintray'

ext.gh = [
	dep: 'https://github.com',
	org: 'opencaesar',
	rep: 'ontologies'
]

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_API_KEY')
	publications = ['maven']
	publish = true
	override = true
	pkg {
		name = project.name
		repo = gh.rep
		userOrg = gh.org
		desc = project.description
		licenses = ['Apache-2.0']
		websiteUrl = gh.dep+'/'+gh.org+'/'+gh.rep
        issueTrackerUrl = gh.dep+'/'+gh.org+'/'+gh.rep+'/issues'
		vcsUrl = gh.dep+'/'+gh.org+'/'+gh.rep+'.git'
		version {
			name = project.version
			desc = project.version
			released  = new Date()
		}
	}

}
