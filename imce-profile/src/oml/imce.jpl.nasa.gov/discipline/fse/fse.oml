@dc:title "Flight System Electrical"
@dc:type "Discipline"
@dc:^description "The Flight System Electrical vocabulary defines concepts and properties for flight system electrical engineering."
@dc:rights """Copyright 2010-2019, by the California Institute of Technology. ALL RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any commercial use must be negotiated with the Office of Technology Transfer at the California Institute of Technology.
This software may be subject to U.S. export control laws. By accepting this software, the user agrees to comply with all applicable U.S. export laws and regulations. User has the responsibility to obtain export licenses,a or other export authority as may be required before exporting such information to foreign countries or providing access to foreign persons."""
@owl:versionInfo "0.5.0"
@dc:creator "Jet Propulsion Laboratory"
@dc:date "2019-11-25"
vocabulary <http://imce.jpl.nasa.gov/discipline/fse/fse> with # as fse {

    extends <http://imce.jpl.nasa.gov/foundation/project>

    extends <http://iso.org/80000-4-magnitudes-1>

    aspect EndCircuitPresentingElement :> mission:PresentingElement

    aspect MassConstrainedElement :> metrology:Object

    ref concept project:WorkPackage :> MassConstrainedElement

    concept Subsystem :> mission:Component, MassConstrainedElement [
        restricts all relation base:aggregates to Assembly
    ]
    
    concept Assembly :> mission:Component, MassConstrainedElement, EndCircuitPresentingElement [
        restricts all relation base:isAggregatedIn to Subsystem
    ]

    concept EndCircuit :> mission:Interface [
        restricts all relation mission:isJoinedIn to Function
        restricts all relation mission:isPresentedBy to EndCircuitPresentingElement
    ]

    concept PowerEndCircuit :> EndCircuit

    concept PowerIn :> PowerEndCircuit

    concept PowerOut :> PowerEndCircuit

    concept Function :> mission:Junction [
        restricts all relation mission:joins to EndCircuit
    ]

    concept PowerFunction :> Function [
        restricts all relation mission:joins to PowerEndCircuit
    ]

    concept MassConstraint :> 80000-4-quantities:mass, 80000-4-magnitudes-1:kilogram-magnitude [
        restricts all relation metrology:isQuantityOf to MassConstrainedElement
    ]

    concept MassAllocationConstraint :> MassConstraint

    concept MassLimitConstraint :> MassConstraint

    relation entity ConnectsAssembly [
        from Function
        to Assembly
        forward connectsAssembly
        asymmetric
        irreflexive
    ]

    relation entity ConnectsAssembly1 :> ConnectsAssembly [
        from Function
        to Assembly
        forward connectsAssembly1
        asymmetric
        irreflexive
    ]

    relation entity ConnectsAssembly2 :> ConnectsAssembly [
        from Function
        to Assembly
        forward connectsAssembly2
        asymmetric
        irreflexive
    ]

    enumerated scalar EndCircuitDirection :> xsd:string [
        "in"^^xsd:string,
        "inout"^^xsd:string,
        "out"^^xsd:string
    ]

    enumerated scalar FunctionDirection :> xsd:string [
        "1to2"^^xsd:string,
        "2to1"^^xsd:string,
        "Bidirectional"^^xsd:string,
        "Undefined"^^xsd:string
    ]

    scalar property hasFunctionDirection [
        domain Function
        range FunctionDirection
    ]
    
    scalar property hasEndCircuitDirectione [
        domain Function
        range EndCircuitDirection
    ]

    scalar property hasEndCircuitType [
        domain Function
        range xsd:string
    ]

}